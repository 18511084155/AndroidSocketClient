apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

//gradle clean build bintrayUpload

group = GROUP_ID
version = VERSION

publishing {
    publications {
        Production(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// gradle bintrayUpload
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = NAME
        desc = PROJECT_DESC
        websiteUrl = WEBSITEURL
        issueTrackerUrl = ISSUE_URL
        vcsUrl = GIT_URL
        licenses = ['Apache-2.0']
        labels = ['android', 'aar']
        publicDownloadNumbers = true
        version {
            name = VERSION
            desc = PROJECT_DESC
            released = new Date()
            vcsTag = VERSION
        }
    }
}